class ScriptRenderer : ZilchComponent
{
  [Property] var Active : Boolean = true;

  function Initialize()
  {
    Script.Connect(this.Space, Events.CollectRenderTasks, this.OnCollectRenderTasks);
  }

  function OnCollectRenderTasks(e : RenderTaskEvent)
  {
    if(!this.Active)
      return;
    
    var finalColorTarget = e.GetFinalTarget(e.ViewportSize, TextureFormat.RGBA8);
    var depthTarget = e.GetRenderTarget(e.ViewportSize, TextureFormat.Depth24Stencil8);

    var clearTargetTask = e.CreateClearTargetRenderTask();
    clearTargetTask.Target = finalColorTarget;
    clearTargetTask.ClearColor = Real4(0.1, 0.2, 0.3, 1);

    var clearDepthTask = e.CreateClearTargetRenderTask();
    clearDepthTask.Target = depthTarget;
    clearDepthTask.Depth = 1;
    
    this.OpaquePass(e, finalColorTarget, depthTarget);
    this.AdditiveBlendPass(e, finalColorTarget, depthTarget);
  }

  function OpaquePass(e : RenderTaskEvent, finalColorTarget : RenderTarget, depthTarget : RenderTarget)
  {
    var settings = RenderPipelineSettings();
    settings.BlendSettings.BlendMode = BlendMode.Disabled;
    var renderGroupTask = e.CreateRenderGroupRenderTask(settings);
    renderGroupTask.ColorTarget0 = finalColorTarget;
    renderGroupTask.DepthTarget = depthTarget;
    renderGroupTask.AddRenderGroup(RenderGroup.Opaque);
  }

  function AdditiveBlendPass(e : RenderTaskEvent, finalColorTarget : RenderTarget, depthTarget : RenderTarget)
  {
    var settings = RenderPipelineSettings();
    settings.BlendSettings.BlendMode = BlendMode.Enabled;
    settings.BlendSettings.ColorSourceFactor = BlendFactor.SourceAlpha;
    settings.BlendSettings.ColorDestFactor = BlendFactor.InvSourceAlpha;
    settings.BlendSettings.ColorBlendEquation = BlendEquation.Add;
    settings.BlendSettings.AlphaSourceFactor = BlendFactor.SourceAlpha;
    settings.BlendSettings.AlphaDestFactor = BlendFactor.InvSourceAlpha;
    settings.BlendSettings.AlphaBlendEquation = BlendEquation.Add;

    var additiveBlendGroupTask = e.CreateRenderGroupRenderTask(settings);
    additiveBlendGroupTask.ColorTarget0 = finalColorTarget;
    additiveBlendGroupTask.DepthTarget = depthTarget;
    additiveBlendGroupTask.AddRenderGroup(RenderGroup.AdditiveBlend);
  }
}
